buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.visiontech.yummysmile"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0" + (System.getenv("BUILD_NUMBER") ? "#" + System.getenv("BUILD_NUMBER") : "")
    }
    buildTypes {
        release {
            minifyEnabled false
            //FIXME Change to release signing config
            //signingConfig signingConfigs.release
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue("string", "app_name", "Yummy Smile")
            buildConfigField("String", "RS_API_URL", "\"http://207.210.66.71/\"")
            buildConfigField("String", "RS_API_APP_NAME", "\"yummysmile/\"")
            ext.betaDistributionReleaseNotes=writeReleaseNotes("Release")
            ext.betaDistributionGroupAliases="Devs,QA"
        }
        debug {
            applicationIdSuffix ".dev"
            resValue("string", "app_name", "Yummy Dev")
            //buildConfigField("String", "RS_API_URL", "\"http://207.210.66.71/\"")
            //buildConfigField("String", "RS_API_APP_NAME", "\"yummysmile/\"")
            buildConfigField("String", "RS_API_URL", "\"http://192.168.0.12/\"")
            buildConfigField("String", "RS_API_APP_NAME", "\"yummy/\"")
            ext.betaDistributionReleaseNotes=writeReleaseNotes("Debug")
            ext.betaDistributionGroupAliases="Devs"
        }
        qa {
            applicationIdSuffix ".qa"
            debuggable true
            signingConfig signingConfigs.debug
            resValue("string", "app_name", "Yummy QA")
            buildConfigField("String", "RS_API_URL", "\"http://207.210.66.71/\"")
            buildConfigField("String", "RS_API_APP_NAME", "\"yummysmile/\"")
            ext.betaDistributionReleaseNotes=writeReleaseNotes("QA")
            ext.betaDistributionGroupAliases="Devs,QA"
        }
    }
    dexOptions {
        preDexLibraries false
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:support-annotations:23.3.0'

    compile 'com.facebook.android:facebook-android-sdk:4.+'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.guava:guava:18.0'

    apt 'com.google.dagger:dagger-compiler:2.0.2'
    compile('com.google.dagger:dagger:2.1') {
        exclude module: 'guava'
    }
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'io.reactivex:rxandroid:1.1.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.0'

    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'



    compile 'com.github.bumptech.glide:glide:3.6.1'

    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
}

check.doLast {
    project.tasks.getByName("checkstyle").execute()
    project.tasks.getByName("findbugs").execute()
    project.tasks.getByName("pmd").execute()
}
